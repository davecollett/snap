# Add Landonline import menu

###################################################################################
# Updated calculate orders script to include option to calculate Landonline 
# files...

menu_item "&File|&Import|&Landonline dumpsql" "Import a landonline adjustment into a new SNAP job"
actions
    ImportLolAdjustment()
end_menu_item

function ImportLolAdjustment()
    $perl = FindPerl()
    if not $perl then exit endif

    $script = "$snap_path\\lol2snap.bat"
    if not FileExists($script) then
       Message("Script file lol2snap.bat is not installed - this function is unavailable","Installation error")
       exit
    endif

    $lol_file = 'clipboard'
    $use_clip = true
    $commandfile = ''

    dialog "Import LOL cadastral adjustment" $imported
    label "Import a Landonline cadastral adjustment into a new SNAP job"
    label "The adjustment DBUG parameter must include \"DUMPSQL\""
    spacer
    open_file_selector $lol_file "Landonline file: Use clipboard or select file" "Landonline report files (*.lst,*.txt)|*.lst;*.txt|All files (*.*)|*.*"
     spacer
     save_file_selector $commandfile "SNAP command file" "Snap command files (*.snp)|*.snp|All files (*.*)|*.*"

    validator
       $use_clip = (($lol_file == 'clipboard') or ($lol_file == ''))
       if (not $use_clip) then 
        if (not $lol_file) then
          message("You must use the clipboard or select an input file","Input file error")
          return false
        elseif not fileexists($lol_file) then
          message("The landonline report file does not exist - please reselect","Input file error")
          return false
        endif
       endif

       if not $commandfile then
          message("You must enter a command file name","Snap command file error")
          return false
       elseif fileexists($commandfile) then
          if not ask("The snap job already exists .. do you want to overwrite it","Overwrite?") then
             return false
          endif
      endif

      $snap_root = SetExtension($commandfile,'')
      $source = $lol_file
      if $use_clip then $source = "-c" endif


      $command = "\"$perl\" \"$script\" \"$source\" \"$snap_root\""

      $perlresult = debuglogrun($command)

      $error = Match($perlresult,'^Error\:\s+(.*)$',1)
      if $error then
         Message($error,"Error importing data")
         return false
      endif

      if( Match($perlresult,'\S') ) then Message($perlresult,"Note") endif
    
      return true
    end_dialog
 
    if $imported then LoadJob($commandfile) endif
   
end_function
