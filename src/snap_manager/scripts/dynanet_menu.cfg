# Add Dynanet XML import menu itme

###################################################################################

menu_item "&File|&Import|&Dynanet XML files" "Import a dynanet xml files into a new SNAP job"
actions
    ImportDynanetXML()
end_menu_item

function ImportDynanetXML()
    $perl = FindPerl()
    if not $perl then exit endif

    $script = "$snap_path\\DynanetXMLToSnap.bat"
    if not FileExists($script) then
       Message("Script file DynanetXMLToSnap.bat is not installed - this function is unavailable","Installation error")
       exit
    endif

    $stnfile = ''
    $obsfile = ''
    $geofile = ''
    $commandfile = ''
    $snaproot = ''
    $message = ''

    dialog "Import Dynanet XML files" $imported
    label "Import Dynanet XML files into a new SNAP job"
    spacer
    open_file_selector $stnfile "Station XML file" "Dynanet xml files (*.xml)|*.xml|All files (*.*)|*.*"
    open_file_selector $geofile "Geoid  file (optional)" "Dynanet xml files (*.xml)|*.xml|All files (*.*)|*.*"
    open_file_selector $obsfile "Observation XML file" "Dynanet xml files (*.xml)|*.xml|All files (*.*)|*.*"
     spacer
     save_file_selector $commandfile "SNAP command file" "Snap command files (*.snp)|*.snp|All files (*.*)|*.*"

    validator
        if (not $stnfile) then
          message("You must use select an XML station file","Input file error")
          return false
        elseif not fileexists($stnfile) then
          message("The XML station file does not exist - please reselect","Input file error")
          return false
        endif

        if (not $obsfile) then
          message("You must use select an XML station file","Input file error")
          return false
        elseif not fileexists($obsfile) then
          message("The XML observation file does not exist - please reselect","Input file error")
          return false
        endif

        if ($geofile) then
         if not fileexists($geofile) then
          message("The geoid file does not exist - please reselect or leave blank","Input file error")
          return false
        endif
        endif

       if (not $commandfile) then
          message("You must enter a command file name","Snap command file error")
          return false
       elseif fileexists($commandfile) then
          if not ask("The snap job already exists .. do you want to overwrite it","Overwrite?") then
             return false
          endif
       endif

      $snaproot = SetExtension($commandfile,'')
      DeleteFile("$snaproot.snp")

      if ($geofile) then 
         $geofile = "\"$geofile\"" 
      endif


      $command = "\"$perl\" \"$script\" \"$stnfile\" $geofile \"$obsfile\" \"$snaproot\""

      $perlresult = debuglogrun($command)

      if $perlresult == 'true' then $perlresult = '' endif
      if $perlresult then 
         $message = "\nOutput from DynanetXML conversion\n$perlresult\n"
      endif
    
      return true
    end_dialog
 
    if fileexists("$snaproot.snp") then LoadJob("$snaproot.snp") endif
    log($message)
    
   
end_function
