# Add Newgan XML import menu itme

###################################################################################

menu_item "&File|&Import|&Newgan file" "Import a newgan adjustment into a new SNAP job"
actions
    ImportNewganXML()
end_menu_item

function ImportNewganXML()
    $perl = FindPerl()
    if not $perl then exit endif

    $script = "$snap_path\\NewganToSnap.bat"
    if not FileExists($script) then
       Message("Script file NewganToSnap.bat is not installed - this function is unavailable","Installation error")
       exit
    endif

    $newganfile = ''
    $commandfile = ''
    $snaproot = ''
    $message = ''

    dialog "Import Newgan file" $imported
    label "Import Newgan file into a new SNAP job"
    spacer
    open_file_selector $newganfile "Newgan file" "Newgan files (*.dat)|*.dat|All files (*.*)|*.*"
     spacer
     save_file_selector $commandfile "SNAP command file" "Snap command files (*.snp)|*.snp|All files (*.*)|*.*"

    validator
        if (not $newganfile) then
          message("You must use select a Newgan file","Input file error")
          return false
        elseif not fileexists($newganfile) then
          message("The Newgan file does not exist - please reselect","Input file error")
          return false
        endif

       if (not $commandfile) then
          message("You must enter a command file name","Snap command file error")
          return false
       elseif fileexists($commandfile) then
          if not ask("The snap job already exists .. do you want to overwrite it","Overwrite?") then
             return false
          endif
       endif

      $snaproot = SetExtension($commandfile,'')
      DeleteFile("$snaproot.snp")

      $command = "\"$perl\" \"$script\" \"$newganfile\" \"$snaproot\""

      $perlresult = debuglogrun($command)

      if $perlresult == 'true' then $perlresult = '' endif

      if fileexists("$snaproot.newgan_snap.log") then
         $message = ReadFile("$snaproot.newgan_snap.log")
      elseif $perlresult then 
         $message = "\nOutput from Newgan conversion\n$perlresult\n"
      endif
    
      return true
    end_dialog
 
    if fileexists("$snaproot.snp") then LoadJob("$snaproot.snp") endif
    log($message)
    
   
end_function
