# Menu items SINEX files

###############################################################################
# function to import a SINEX file into the job

function importSinex()
    $perl=FindPerl()
    if not $perl then exit endif

    dialog "Import a SINEX file into the adjustment"
    label "Create a SNAP data file by importing data from a SINEX file\n"
    open_file_selector $snxfile "Sinex file" "Sinex files (*.snx)|*.snx|All files (*.*)|*.*"
    save_file_selector $datfile "SNAP data file" "Data files (*.dat)|*.dat|All files (*.*)|*.*"
    validator
       $dfname=Filename($datfile,"relative",$job_path)
       if not FileExists($snxfile) then
          message("You must enter a valid SINEX file name","Input error")
          return false
       elseif not $datfile then
          message("You must enter a SNAP data file ","Input error")
          return false
       elseif FileExists($datfile) then
          if not ask("The SNAP data file $dfname already exists.\nDo you want to overwrite it","Overwrite?") then
             return false
          endif
       endif
       return true
    end_dialog
  
    log("Importing $snxfile to $dfname")
    DeleteFile($datfile)
    $command="snx2snap.bat \"$snxfile\" \"$datfile\""
    $snxresult=debuglogrun( $command )
    if not FileExists($datfile) then
        Message("Unable to create data file $datfile\n$snxresult","Import error")
        return false
    endif
    AddDataFile($dfname,'')
    log("Data file $datfile added to job")
    return true
end_function

menu_item "&Data|&Import|&SINEX file" "Import a sinex file to the SNAP job"
requirements
    return $job_valid
actions
    importSinex()
end_menu_item

